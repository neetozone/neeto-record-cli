@echo off
setlocal

set "BASE_URL=https://neeto-downloads.s3.amazonaws.com/cli/NeetoRecord/latest"
set "INSTALL_DIR=%LOCALAPPDATA%\Programs\neetorecord"
if defined NEETORECORD_INSTALL_DIR set "INSTALL_DIR=%NEETORECORD_INSTALL_DIR%"

set "ARCH=amd64"
if "%PROCESSOR_ARCHITECTURE%"=="ARM64" set "ARCH=arm64"
set "ARCHIVE=neetorecord_windows_%ARCH%.zip"

echo Downloading NeetoRecord CLI...
set "TMPDIR=%TEMP%\neetorecord-install"
if exist "%TMPDIR%" rmdir /s /q "%TMPDIR%"
mkdir "%TMPDIR%"

curl -fsSL "%BASE_URL%/%ARCHIVE%" -o "%TMPDIR%\%ARCHIVE%"
if %errorlevel% neq 0 (
    echo Failed to download NeetoRecord CLI.
    rmdir /s /q "%TMPDIR%"
    exit /b 1
)

echo Verifying checksum...
curl -fsSL "%BASE_URL%/SHA256SUMS" -o "%TMPDIR%\SHA256SUMS"
if %errorlevel% neq 0 (
    echo Failed to download the checksum file.
    rmdir /s /q "%TMPDIR%"
    exit /b 1
)

set "EXPECTED="
for /f "usebackq tokens=1,2" %%A in ("%TMPDIR%\SHA256SUMS") do (
    if /i "%%B"=="%ARCHIVE%" set "EXPECTED=%%A"
    if /i "%%B"=="*%ARCHIVE%" set "EXPECTED=%%A"
)
if not defined EXPECTED (
    echo No published checksum for %ARCHIVE%. Aborting.
    rmdir /s /q "%TMPDIR%"
    exit /b 1
)

set "ACTUAL="
for /f "skip=1 tokens=*" %%H in ('certutil -hashfile "%TMPDIR%\%ARCHIVE%" SHA256') do (
    if not defined ACTUAL set "ACTUAL=%%H"
)
set "ACTUAL=%ACTUAL: =%"
if /i not "%ACTUAL%"=="%EXPECTED%" (
    echo Checksum mismatch for %ARCHIVE%. Aborting.
    echo   expected: %EXPECTED%
    echo   actual:   %ACTUAL%
    rmdir /s /q "%TMPDIR%"
    exit /b 1
)

echo Extracting...
powershell -Command "Expand-Archive -Path '%TMPDIR%\%ARCHIVE%' -DestinationPath '%TMPDIR%' -Force"

echo Installing to %INSTALL_DIR%...
if not exist "%INSTALL_DIR%" mkdir "%INSTALL_DIR%"
copy /y "%TMPDIR%\neetorecord.exe" "%INSTALL_DIR%\neetorecord.exe" >nul

echo %PATH% | findstr /i /c:"%INSTALL_DIR%" >nul
if %errorlevel% neq 0 (
    for /f "tokens=2*" %%A in ('reg query "HKCU\Environment" /v Path 2^>nul') do set "USER_PATH=%%B"
    setx PATH "%USER_PATH%;%INSTALL_DIR%" >nul
    echo Added %INSTALL_DIR% to user PATH.
)

rmdir /s /q "%TMPDIR%"

echo NeetoRecord CLI installed successfully. Restart your terminal and run 'neetorecord --help' to get started.
